#
# Switches between the front search and result mode.
#
# Front search is the view that the user sees when no query has
# been submitted, the result mode is shown when there is a query
# and search results to show.
# 
toggleSearchMode = () ->
	if $('#query').val() == ''
		$('.large-logo').show()
		$('.small-logo').hide()
		$('#loading-results').hide()
		$('#query').removeClass 'small'
	else
		$('.large-logo').hide()
		$('.small-logo').show()
		$('#loading-results').show()
		$('#query').addClass 'small'
		
#
# Perform a search and fetch results.
#
search = (id) ->
	$.ajax {
		url: "/search/fetch?id=#{id}",
		method: 'get',
		success: (html, status, jqXHR) ->
			showResults html
		error: (jqXHR, status, error) ->
			showError error
	}
		
showResults = (html) ->
	$('#loading-results').hide()
	$('#alert').hide()
	$('#results').html(html)
		
showError = (error) ->
	$('#loading-results').hide()
	$('#results').hide()
	$('#alert_message').html(error)
	$('#alert').show()
	
refreshPlaceholder = ->
	activeTab = $('.tab.active').data('tab')
	placeholder = $("#placeholder_#{activeTab}").val()
	$('#query').attr 'placeholder', placeholder
	
refreshCategories = ->
	categories = $('.tab.active').data('categories')
	$('#categories').val categories
	
changeCategory = ->
	categories = $('.tab.active').data('categories')
	tab = $('.tab.active').data('tab')
	query = $('#query').val()
	newUrl = "/search?q=#{query}&c=#{categories}##{tab}"
	window.location.href = newUrl
	
$(document).on 'contentReady', ->
	
	$('#paginator a').when 'click.search', ->
		$('#loading-results').show()
		$('#results').hide()
		$(window).scrollTop($('#query').offset().top-40)

	$('#paginator a').when('ajax:success', (e, data, status, xhr) ->
		$('#loading-results').hide()
		$('#alert').hide()
		$('#results').html(data)
		$('#results').show()
		
	).when('ajax:error.search', (e, xhr, status, error) ->
		$('#loading-results').hide()
		$('#results').hide()
		$('#alert_message').html(error)
		$('#alert').show()
	)

$ ->
	$('#query').autocomplete({
		source: "/search/suggest.json",
		minLength: 2,
		appendTo: '#form form'
		focus: (event,ui) ->
			$('#query').val(ui.item.query)
			false
		select: (event,ui) ->
			$('#search form').submit()
			false
	})
	.autocomplete('instance')._renderItem = (ul, item) ->
		l = $('#query').val().length
		str = item.query[0..l-1] + '<b>' + item.query[l..]
		$('<li>'+str+'</b></li>').appendTo(ul)
		
	search_id = $('#search_id').val()
	if search_id
		search search_id
		
	toggleSearchMode()
	refreshPlaceholder()
	refreshCategories()
	
	$(document).on 'click', '#search .tab', (event) ->
		if $('#query').val()
			changeCategory()
		else
			refreshPlaceholder()
			refreshCategories()
		